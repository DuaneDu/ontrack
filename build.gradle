import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.netflix.gradle.plugins.deb.Deb
import com.netflix.gradle.plugins.packaging.SystemPackagingTask
import com.netflix.gradle.plugins.rpm.Rpm
import org.springframework.boot.gradle.plugin.SpringBootPlugin
import org.redline_rpm.header.Os

buildscript {
    ext {
        kotlinVersion = '1.3.50'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:3.0.1'
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

plugins {
    id 'net.nemerosa.versioning' version '2.7.1'
    id "nebula.deb" version "6.2.1"
    id "nebula.rpm" version "6.2.1"
    id 'org.sonarqube' version '2.5'
    id "com.avast.gradle.docker-compose" version "0.9.5"
    id "com.bmuschko.docker-remote-api" version "4.1.0"
    id "org.springframework.boot" version "2.1.8.RELEASE" apply false
}

/**
 * Profiles
 */

ext {
    documentationProfile = project.hasProperty('documentation')
}

/**
 * Meta information
 */

allprojects {
    group = 'net.nemerosa.ontrack'
    version = versioning.info.display
}

/**
 * Resolution
 */

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}


/**
 * Integration test environment
 */

// Pre-integration tests: starting Postgresql

dockerCompose {
    integrationTest {
        useComposeFiles = ["${rootDir}/gradle/it/docker-compose.yml"]
        projectName = "ontrack_it"
        forceRecreate = true
    }
}

task preIntegrationTest {
    dependsOn integrationTestComposeUp
    // When done
    doLast {
        def host = integrationTestComposeUp.servicesInfos.db.firstContainer.host as String
        def port = integrationTestComposeUp.servicesInfos.db.firstContainer.ports.get(5432) as int
        project.ext.jdbcUrl = "jdbc:postgresql://$host:$port/ontrack"
        logger.info("Pre integration test JDBC URL = ${project.ext.jdbcUrl}")
    }
}

// Post-integration tests: stopping Postgresql

task postIntegrationTest {
    dependsOn integrationTestComposeDown
}

/**
 * Java projects
 */

ext {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

def javaProjects = subprojects.findAll {
    it.path != ':ontrack-web'
}

def coreProjects = javaProjects.findAll {
    it.path != ':ontrack-dsl'
}

configure(javaProjects) {

    /**
     * For all Java projects
     */

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    // Javadoc

    if (documentationProfile) {
        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        // Sources

        task sourceJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }
    }

    // POM file

    publishing {
        publications {
            mavenCustom(MavenPublication) {
                from components.java
                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', project.name)
                    root.appendNode('description', project.description ?: project.name)
                    root.appendNode('url', 'http://nemerosa.github.io/ontrack')

                    def licenses = root.appendNode('licenses')
                    def license = licenses.appendNode('license')
                    license.appendNode('name', 'The MIT License (MIT)')
                    license.appendNode('url', 'http://opensource.org/licenses/MIT')
                    license.appendNode('distribution', 'repo')

                    def scm = root.appendNode('scm')
                    scm.appendNode('url', 'https://github.com/nemerosa/ontrack')
                    scm.appendNode('connection', 'scm:git://github.com/nemerosa/ontrack')
                    scm.appendNode('developerConnection', 'scm:git://github.com/nemerosa/ontrack')

                    def developers = root.appendNode('developers')
                    def developer = developers.appendNode('developer')
                    developer.appendNode('id', 'dcoraboeuf')
                    developer.appendNode('name', 'Damien Coraboeuf')
                    developer.appendNode('email', 'damien.coraboeuf@gmail.com')
                }
            }
        }
    }

    model {
        ValidationRunWithAutoStampIT
        tasks.generatePomFileForMavenCustomPublication {
            destination = file("${buildDir}/poms/${project.name}-${version}.pom")
        }
    }

    afterEvaluate {
        tasks.assemble.dependsOn 'generatePomFileForMavenCustomPublication'
    }

    // Archives for Javadoc and Sources

    artifacts {
        if (documentationProfile) {
            archives javadocJar
            archives sourceJar
        }
    }

}

configure(coreProjects) {

    /**
     * For all Java projects
     */

    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom(SpringBootPlugin.BOM_COORDINATES) {
                bomProperty("kotlin.version", kotlinVersion)
            }
        }
        dependencies {
            dependency 'commons-io:commons-io:2.6'
            dependency 'org.apache.commons:commons-text:1.6'
            dependency 'net.jodah:failsafe:1.1.1'
            dependency 'commons-logging:commons-logging:1.2'
            dependency 'org.apache.commons:commons-math3:3.6.1'
            dependency 'com.google.guava:guava:27.0.1-jre'
            dependency 'args4j:args4j:2.33'
            dependency 'org.jgrapht:jgrapht-core:1.3.0'
            dependency 'org.kohsuke:groovy-sandbox:1.19'
            dependency 'com.graphql-java:graphql-java:11.0'
            dependency "org.jetbrains.kotlin:kotlin-test:${kotlinVersion}"
            // Overrides from Spring Boot
            dependency 'org.postgresql:postgresql:9.4.1208'
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
        compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
        compileOnly 'org.projectlombok:lombok:1.18.10'
        annotationProcessor 'org.projectlombok:lombok:1.18.10'
        testCompileOnly 'org.projectlombok:lombok:1.18.10'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'
        // Testing
        testCompile 'junit:junit'
        testCompile 'org.mockito:mockito-core'
        testCompile 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
        testCompile "org.jetbrains.kotlin:kotlin-test"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    // Unit tests run with the `test` task
    test {
        include '**/*Test.class'
        reports {
            html.enabled = false
        }
    }

    // Integration tests
    task integrationTest(type: Test) {
        mustRunAfter 'test'
        include '**/*IT.class'
        minHeapSize = "512m"
        maxHeapSize = "1024m"
        dependsOn preIntegrationTest
        finalizedBy postIntegrationTest
        /**
         * Sets the JDBC URL
         */
        doFirst {
            println "Setting JDBC URL for IT: ${rootProject.ext.jdbcUrl}"
            systemProperty 'spring.datasource.url', rootProject.ext.jdbcUrl
            systemProperty 'spring.datasource.username', 'ontrack'
            systemProperty 'spring.datasource.password', 'ontrack'
        }
    }

    // Synchronization with shutting down the database
    rootProject.integrationTestComposeDown.mustRunAfter project.integrationTest

    // Acceptance tests
    task acceptanceTest(type: Test) {
        mustRunAfter 'integrationTest'
        include '**/ACC*.class'
    }

}

/**
 * Packaging for delivery
 */

apply from: 'gradle/packaging.gradle'

/**
 * Packaging for OS
 *
 * The package version does not accept versions like the ones generated
 * from the Versioning plugin for the feature branches for example.
 */

def packageVersion
if (version ==~ /\d+\.\d+\.\d+/) {
    packageVersion = version.replaceAll(/[^0-9\.-_]/, '')
} else {
    packageVersion = '0.0.0'
}
println "Using package version = ${packageVersion}"

task debPackage(type: Deb) {
    dependsOn(":ontrack-ui:bootJar")

    link("/etc/init.d/ontrack", "/opt/ontrack/bin/ontrack.sh")
}

task rpmPackage(type: Rpm) {
    dependsOn(":ontrack-ui:bootJar")

    user = "ontrack"
    link("/etc/init.d/ontrack", "/opt/ontrack/bin/ontrack.sh")
}

tasks.withType(SystemPackagingTask) {

    packageName = "ontrack"
    release = "1"
    version = packageVersion
    os = Os.LINUX // only applied to RPM

    preInstall("gradle/os-package/preInstall.sh")
    postInstall("gradle/os-package/postInstall.sh")

    from(project(":ontrack-ui").file("build/libs")) {
        include("ontrack-ui-${project.version}.jar")
        into("/opt/ontrack/lib")
        rename(".*", "ontrack.jar")
    }

    from("gradle/os-package") {
        include("ontrack.sh")
        into("/opt/ontrack/bin")
        fileMode = 0x168 // 0550
    }
}

task osPackages {
    dependsOn(rpmPackage)
    dependsOn(debPackage)
}

/**
 * Docker tasks
 */

task dockerPrepareEnv(type: Copy, dependsOn: ':ontrack-ui:bootJar') {
    from 'ontrack-ui/build/libs'
    include '*.jar'
    exclude '*-javadoc.jar'
    exclude '*-sources.jar'
    into project.file('docker')
    rename '.*', 'ontrack.jar'
}

task dockerBuild(type: DockerBuildImage) {
    dependsOn dockerPrepareEnv
    inputDir = file("docker")
    tags.add("nemerosa/ontrack:$version")
    tags.add("nemerosa/ontrack:latest")
}

/**
 * Acceptance tasks
 */

apply from: 'gradle/acceptance.gradle'

/**
 * Development tasks
 */

dockerCompose {
    dev {
        useComposeFiles = ["${rootDir}/gradle/compose/docker-compose-dev.yml"]
        projectName = "dev"
        forceRecreate = false
        environment.put("POSTGRES_NAME", project.properties.devPostgresName)
        environment.put("POSTGRES_PORT", project.properties.devPostgresPort)
    }
}

task devStart {
    dependsOn devComposeUp
}

task devStop {
    dependsOn devComposeDown
}

/**
 * Publication tasks
 *
 * Standalone Gradle tasks in `gradle/publication.gradle` and in
 * `gradle/production.gradle`
 */

/**
 * General test report
 */

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` tasks in all core subprojects
    reportOn coreProjects*.test, coreProjects*.integrationTest, coreProjects*.acceptanceTest
}
