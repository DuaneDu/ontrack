/**
 * CI environment
 */

import net.nemerosa.ontrack.gradle.*

dockerCompose {
    ci {
        useComposeFiles = ["${rootDir}/gradle/compose/docker-compose.yml", "${rootDir}/gradle/compose/docker-compose-ci.yml"]
        projectName = "ci"
        forceRecreate = true
        tcpPortsToIgnoreWhenWaiting = [8083, 8086]
    }
}

task ciStart {
    dependsOn ciComposeUp
    // When done
    doLast {
        def host = ciComposeUp.servicesInfos.ontrack.firstContainer.host as String
        def port = ciComposeUp.servicesInfos.ontrack.firstContainer.ports.get(8080) as int
        ext.ontrackUrl = "https://$host:$port"
        logger.info("Ontrack URL = ${ext.ontrackUrl}")
    }
}

task ciStop {
    dependsOn ciComposeDown
}

task ciAcceptanceTest(type: RemoteAcceptanceTest) {
    acceptanceUrl = { ciStart.ontrackUrl }
    disableSsl = true
    acceptanceTimeout = 300
    acceptanceImplicitWait = 30
    dependsOn ciStart
    finalizedBy ciStop
}

ciComposeDown.mustRunAfter ciAcceptanceTest
